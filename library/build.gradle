apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka-android'

def versionNameConst = "1.0.1"

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 26
        versionCode 2
        versionName versionNameConst

        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath = true
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    configurations {
        javadocDeps
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            configuration = configurations.archives

            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Poliwhirl'
                packaging 'aar'
                // optionally artifactId can be defined here
                description 'Library for searching nice color to show behind the picture'
                url 'https://github.com/FlashLight13/poliwhirl'

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                scm {
                    url 'http://github.com:FlashLight13/poliwhirl.git'
                    connection 'scm:git:git://github.com:FlashLight13/poliwhirl.git'
                    developerConnection 'scm:git:git://github.com:FlashLight13/poliwhirl.git'
                }

                developers {
                    developer {
                        id 'flashlight'
                        name 'Anton Potapov'
                        email 'potapov.a.p13@gmail.com'
                    }
                }
            }
        }
    }
}

dokka {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.5.1'

    compile 'com.android.support:support-annotations:27.0.0'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
}
repositories {
    mavenCentral()
    google()
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from dokka.outputDirectory
}

artifacts {
    group = "com.antonpotapov"
    archivesBaseName = "poliwhirl"
    version = versionNameConst

    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}
